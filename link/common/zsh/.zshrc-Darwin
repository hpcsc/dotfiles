eval "$(/opt/homebrew/bin/brew shellenv)"

# below lines put GNU coreutils/gnu-sed aliases (the ones without `g` prefix) in the PATH
# however if we use prezto, those aliases are not needed because prezto gnu-utility already creates wrappers
# that call `g` binaries. Ref: https://github.com/sorin-ionescu/prezto/blob/master/modules/gnu-utility/init.zsh
# export PATH="$(brew --prefix coreutils)/libexec/gnubin:$(brew --prefix gnu-sed)/libexec/gnubin:$PATH"
# export MANPATH="$(brew --prefix coreutils)/libexec/gnuman:$(brew --prefix gnu-sed)/libexec/gnubin:$MANPATH"

ssh-add --apple-use-keychain &> /dev/null

new_fsec_keychain_if_not_exist() {
  if [ -f ~/Library/Keychains/fsec-db ]; then
    return
  fi

  security create-keychain fsec
  echo "=== created new keychain fsec"
}

fsec() {
  # assume jq, keychain-parser are available
  local sub_command=$1
  case "${sub_command}" in
    list)
      new_fsec_keychain_if_not_exist
      security dump-keychain fsec | keychain-parser | jq -r '(["Service","Account"] | (., map(length*"-"))),(. // [] | .[] | select(.attribute == "fsec") | [.service,.account]) | @tsv' | column -ts$'\t'
      ;;
    add)
      echo -n "service: "
      read svc
      if [ -z "${svc}" ]; then
        return
      fi

      echo -n "account: "
      read acc
      if [ -z "${acc}" ]; then
        return
      fi

      echo -n "secret: "
      IFS= read -rs secret
      if [ -z "${secret}" ]; then
        return
      fi

      secret=$(echo "${secret}" | tr -d '\n')

      new_fsec_keychain_if_not_exist
      security add-generic-password -U -s "${svc}" -a "${acc}" -G fsec -w "${secret}" fsec
      echo "\n=== added secret for service ${svc} and account ${acc} to keychain"
      ;;
    delete)
      new_fsec_keychain_if_not_exist
      local svc=$(security dump-keychain fsec | keychain-parser | jq -r '.[] | select(.attribute == "fsec") | .service' | fzf)
      if [ -z "${svc}" ]; then
        return
      fi

      local acc=$(security dump-keychain fsec | keychain-parser | jq -r '.[] | select(.service == "'${svc}'") | .account' | fzf)
      if [ -z "${acc}" ]; then
        return
      fi

      security delete-generic-password -s "${svc}" -a "${acc}" fsec
      echo "\n=== deleted secret for service ${svc} and account ${acc} from keychain"
      ;;
    *)
      new_fsec_keychain_if_not_exist
      local svc=$(security dump-keychain fsec | keychain-parser | jq -r '[ .[] | select(.attribute == "fsec") ] | unique_by(.service)[] | .service' | fzf)
      if [ -z "${svc}" ]; then
        return
      fi

      local acc=$(security dump-keychain fsec | keychain-parser | jq -r '.[] | select(.service == "'${svc}'") | .account' | fzf)
      if [ -z "${acc}" ]; then
        return
      fi

      security 2>&1 >/dev/null find-generic-password -g -s ${svc} -a ${acc} fsec | grep password | sed 's/password:[[:space:]]"\(.*\)"/\1/g' | tr -d '\n' | pbcopy
      echo "=== copied secret for service ${svc} and account ${acc} to clipboard"
  esac
}
